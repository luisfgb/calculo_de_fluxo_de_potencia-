clear

! IEEE 4-bus test case   Y-Y Stepdown Balanced
! Based on script developed by Alan Dunn and Steve Sparling

new circuit.4BusYYbal basekV=12.47 phases=3 
! **** HAVE TO STIFFEN THE SOURCE UP A LITTLE; THE TEST CASE ASSUMES AN INFINITE BUS
~ mvasc3=200000 200000

set earthmodel=carson

! **** DEFINE WIRE DATA 
new wiredata.conductor Runits=mi Rac=0.306 GMRunits=ft GMRac=0.0244  Radunits=in Diam=0.721 
new wiredata.neutral   Runits=mi Rac=0.592 GMRunits=ft GMRac=0.00814 Radunits=in Diam=0.563 

! **** DEFINE LINE GEOMETRY; REDUCE OUT THE NEUTRAL
new linegeometry.4wire nconds=4 nphases=3 reduce=yes 
~ cond=1 wire=conductor units=ft x=-4   h=28 
~ cond=2 wire=conductor units=ft x=-1.5 h=28 
~ cond=3 wire=conductor units=ft x=3    h=28 
~ cond=4 wire=neutral   units=ft x=0    h=24 

! **** 12.47 KV LINE
new line.line1 geometry=4wire length=2000 units=ft bus1=sourcebus bus2=n2

! **** 3-PHASE STEP-DOWN TRANSFORMER 12.47/4.16 KV Y-Y
new transformer.t1 xhl=6
~ wdg=1 bus=n2 conn=wye kV=12.47 kVA=6000 %r=0.5 
~ wdg=2 bus=n3 conn=wye kV=4.16  kVA=6000 %r=0.5 

! **** 4.16 KV LINE
new line.line2 bus1=n3 bus2=n4 geometry=4wire length=2500 units=ft  

!loadshape
New loadshape.residencial npts=24 interval=1
~ mult=(file=loadshapeR.csv)

Redirect pv_Res_python.dss

! **** WYE-CONNECTED 4.16 KV LOAD
new load.load1 phases=3 bus1=n4 conn=wye kV=4.16 kW=5400 pf=0.9  model=1 daily=residencial
! **** HAVE TO ALLOW P, Q TO REMAIN CONSTANT TO ABOUT .79 PU -- THIS IS ASSUMED IN TEST CASE
! **** DEFAULT IN DSS IS .95, BELOW WHICH IT REVERTS TO LINEAR MODEL
~ vminpu=0.75    ! model will remain const p,q down to 0.75 pu voltage  

!energy meters
new energymeter.medidorSource element=line.line1 terminal=1


!monitors
new monitor.Linha1_power element=line.line1 terminal=1 mode=1 ppolar=no
new monitor.Linha1_V element=line.line1 terminal=1 mode=0
new monitor.Linha2power element=line.line2 terminal=1 mode=1 ppolar=no
new monitor.Linha2_V element=line.line2 terminal=1 mode = 0
new monitor.TransfP element=transformer.t1 terminal=1 mode=1 ppolar=no
new monitor.TransfV element=transformer.t1 terminal=1 mode=0
new monitor.Carga_power element=load.load1 terminal=1 mode=1 ppolar=no
new monitor.Carga_V element=load.load1 terminal=1 mode=0

New monitor.PV_power element=PVSystem.Pv terminal=1 mode=1 ppolar=no
New monitor.PV_voltage element=PVSystem.Pv terminal=1 mode=0 
New monitor.PV_variables element=PVSystem.Pv terminal=1 mode=3 

set voltagebases=[12.47, 4.16] 
calcvoltagebases     ! **** let DSS compute voltage bases

set mode=daily
set stepsize=1h

